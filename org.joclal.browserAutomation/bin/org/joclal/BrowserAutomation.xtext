grammar org.joclal.BrowserAutomation with org.eclipse.xtext.common.Terminals

generate browserAutomation "http://www.joclal.org/BrowserAutomation"

BrowserAutomation:
	subroutines+=Subroutine*
	'browser' browser=Browser ';'
	firstGoTo=Goto
	actions+=Action*
;
	
enum Browser:
	chrome='Chrome' | safari='Safari' | opera='Opera' | firefox='Firefox' | ie='Internet Explorer'
;

Action:
	Goto | ClickOn | Fill | Check | Uncheck | Pick | Let | DoWhile | IfThen | SubroutineCall
;

Goto:
	'GoTo' url=Value ';'
;

ClickOn:
	'Click' selector=Selector ';'
;

Selector:
	'select(' id=STRING ')'
;

Fill:
	'Fill' field=Selector 'with' value=Value ';'
;

Check:
	'Check' checkbox=Selector ';'
;

Uncheck:
	'Uncheck' checkbox=Selector ';'
;

Pick:
	'Pick' value=STRING 'in' select=Selector ";"
;

Let:
	'Let' id=VariableId '=' value=LetValue ';'
;

LetValue:
	selector=Selector | value=Value
;

Value:
	string=STRING | variable=[VariableId]
;

DoWhile:
	'Do' '{' 
		actions+=Action* 
	'}' 'While' '(' 
		condition=BooleanExp 
	')' 
;

BooleanExp:
	leftMember=Value operator=Operator rightMember=Value
;

enum Operator:
	equals='==' | notEquals='!=' | gt='>' | lt='<' | gte='>=' | lte='<=' 
;

IfThen:
	'If' '(' 
		condition=BooleanExp
	')' 'Then' '{'
		thenActions+=Action*
	'}' ('Else' '{'
		elseActions+=Action*
	'}')?
;

Subroutine:
	'Def' name=ID 
	('(' 
		(params+=VariableId (',' params+=VariableId)*)?
	')')?
	'{'
		(actions+=Action)*
	'}'
;

SubroutineCall:
	'Call' subroutine=[Subroutine] '(' (params+=Value (',' params+=Value)*)? ')'';'
;

VariableId:
	name=ID
;